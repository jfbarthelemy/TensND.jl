abstract type AbstractBasis{dim,T<:Number} <: AbstractMatrix{T} end

@pure Base.size(::AbstractBasis{dim}) where {dim} = (dim, dim)
Base.getindex(â„¬::AbstractBasis, i::Integer, j::Integer) = getindex(vecbasis(â„¬, :cov), i, j)
@pure Base.eltype(::Type{AbstractBasis{dim,T}}) where {dim,T} = T
@pure getdim(::AbstractBasis{dim}) where {dim} = dim

function subscriptnumber(i::Integer)
    if i < 0
        c = [Char(0x208B)]
    else
        c = []
    end
    for d in reverse(digits(abs(i)))
        push!(c, Char(0x2080+d))
    end
    return join(c)
end

function superscriptnumber(i::Integer)
    if i < 0
        c = [Char(0x207B)]
    else
        c = []
    end
    for d in reverse(digits(abs(i)))
        if d == 0 push!(c, Char(0x2070)) end
        if d == 1 push!(c, Char(0x00B9)) end
        if d == 2 push!(c, Char(0x00B2)) end
        if d == 3 push!(c, Char(0x00B3)) end
        if d > 3 push!(c, Char(0x2070+d)) end
    end
    return join(c)
end


"""
    Basis(v::AbstractMatrix{T}, ::Val{:cov})
    Basis{dim, T<:Number}()
    Basis(Î¸::T<:Number, Ï•::T<:Number, Ïˆ::T<:Number)

Basis built from a square matrix `v` where columns correspond either to
- primal vectors ie `eáµ¢=v[:,i]` if `var=:cov` as by default
- dual vectors ie `eâ±=v[:,i]` if `var=:cont`.

Basis without any argument refers to the canonical basis (`CanonicalBasis`) in `Ráµˆâ±áµ` (by default `dim=3` and `T=Sym`)

Basis can also be built from Euler angles (`RotatedBasis`) `Î¸` in 2D and `(Î¸, Ï•, Ïˆ)` in 3D

The attributes of this object can be obtained by
- `vecbasis(â„¬, :cov)`: square matrix defining the primal basis `eáµ¢=e[:,i]`
- `vecbasis(â„¬, :cont)`: square matrix defining the dual basis `eâ±=E[:,i]`
- `metric(â„¬, :cov)`: square matrix defining the covariant components of the metric tensor `gáµ¢â±¼=eáµ¢â‹…eâ±¼=g[i,j]`
- `metric(â„¬, :cont)`: square matrix defining the contravariant components of the metric tensor `gâ±Ê²=eâ±â‹…eÊ²=gâ±Ê²[i,j]`

# Examples
```julia
julia> v = Sym[1 0 0; 0 1 0; 0 1 1] ; â„¬ = Basis(v)
Basis{3, Sym}
# basis: 3Ã—3 Tensor{2, 3, Sym, 9}:
 1  0  0
 0  1  0
 0  1  1
# dual basis: 3Ã—3 Tensor{2, 3, Sym, 9}:
 1  0   0
 0  1  -1
 0  0   1
# covariant metric tensor: 3Ã—3 SymmetricTensor{2, 3, Sym, 6}:
 1  0  0
 0  2  1
 0  1  1
# contravariant metric tensor: 3Ã—3 SymmetricTensor{2, 3, Sym, 6}:
 1   0   0
 0   1  -1
 0  -1   2

julia> Î¸, Ï•, Ïˆ = symbols("Î¸, Ï•, Ïˆ", real = true) ; â„¬Ê³ = Basis(Î¸, Ï•, Ïˆ) ; display(vecbasis(â„¬Ê³, :cov))
3Ã—3 Tensor{2, 3, Sym, 9}:
 -sin(Ïˆ)â‹…sin(Ï•) + cos(Î¸)â‹…cos(Ïˆ)â‹…cos(Ï•)  -sin(Ïˆ)â‹…cos(Î¸)â‹…cos(Ï•) - sin(Ï•)â‹…cos(Ïˆ)  sin(Î¸)â‹…cos(Ï•)
  sin(Ïˆ)â‹…cos(Ï•) + sin(Ï•)â‹…cos(Î¸)â‹…cos(Ïˆ)  -sin(Ïˆ)â‹…sin(Ï•)â‹…cos(Î¸) + cos(Ïˆ)â‹…cos(Ï•)  sin(Î¸)â‹…sin(Ï•)
                        -sin(Î¸)â‹…cos(Ïˆ)                          sin(Î¸)â‹…sin(Ïˆ)         cos(Î¸)
```
"""
struct Basis{dim,T} <: AbstractBasis{dim,T}
    eáµ¢::Matrix{T} # Primal basis `eáµ¢=eáµ¢[:,i]`
    eâ±::Matrix{T} # Dual basis `eâ±=eâ±[:,i]`
    gáµ¢â±¼::Symmetric{T,Matrix{T}} # Metric tensor `gáµ¢â±¼=eáµ¢â‹…eâ±¼=gáµ¢â±¼[i,j]`
    gâ±Ê²::Symmetric{T,Matrix{T}} # Inverse of the metric tensor `gâ±Ê²=eâ±â‹…eÊ²=gâ±Ê²[i,j]`
    function Basis(
        eáµ¢::AbstractMatrix{T},
        eâ±::AbstractMatrix{T},
        gáµ¢â±¼::AbstractMatrix{T},
        gâ±Ê²::AbstractMatrix{T},
    ) where {T}
        dim = size(eáµ¢, 1)
        @assert dim == size(eáµ¢, 2) "v should be a square matrix"
        if isidentity(eáµ¢)
            return CanonicalBasis{dim,T}()
        elseif isidentity(gáµ¢â±¼)
            return RotatedBasis(eáµ¢)
        elseif isdiag(gáµ¢â±¼)
            Ï‡ = sqrt.(diag(gáµ¢â±¼))
            return OrthogonalBasis(RotatedBasis(eáµ¢ .* transpose(inv.(Ï‡))), Ï‡)
        else
            eáµ¢ = Matrix(eáµ¢)
            gáµ¢â±¼ = Symmetric(gáµ¢â±¼)
            gâ±Ê² = Symmetric(gâ±Ê²)
            eâ± = Matrix(eâ±)
            new{dim,T}(eáµ¢, eâ±, gáµ¢â±¼, gâ±Ê²)
        end
    end
    function Basis(â„¬::AbstractBasis{dim,T}, Ï‡áµ¢::V) where {dim,T,V}
        Î§ = collect(Ï‡áµ¢)
        invÎ§ = inv.(Î§)
        if Î§ == one.(Î§)
            return â„¬
        else
            if â„¬ isa OrthonormalBasis
                return OrthogonalBasis(â„¬, Î§)
            else
                eáµ¢ = vecbasis(â„¬, :cov) .* transpose(Î§)
                eâ± = vecbasis(â„¬, :cont) .* transpose(invÎ§)
                gáµ¢â±¼ = Symmetric(Î§ .* metric(â„¬, :cov) .* transpose(Î§))
                gâ±Ê² = Symmetric(invÎ§ .* metric(â„¬, :cont) .* transpose(invÎ§))
                new{dim,T}(eáµ¢, eâ±, gáµ¢â±¼, gâ±Ê²)
            end
        end
    end
    function Basis(eáµ¢::AbstractMatrix{T}, ::Val{:cov}) where {T}
        dim = size(eáµ¢, 1)
        @assert dim == size(eáµ¢, 2) "v should be a square matrix"
        if isidentity(eáµ¢)
            return CanonicalBasis{dim,T}()
        else
            eáµ¢ = Matrix(eáµ¢)
            gáµ¢â±¼ = tsimplify(Symmetric(transpose(eáµ¢) * eáµ¢))
            if isidentity(gáµ¢â±¼)
                return RotatedBasis(eáµ¢)
            elseif isdiagonal(gáµ¢â±¼)
                Ï‡ = sqrt.(diag(gáµ¢â±¼))
                return OrthogonalBasis(RotatedBasis(eáµ¢ .* transpose(inv.(Ï‡))), Ï‡)
            else
                if T == Sym
                    gâ±Ê² = tsimplify(Symmetric(inv(Matrix(gáµ¢â±¼))))
                else
                    gâ±Ê² = tsimplify(inv(gáµ¢â±¼))
                end
                eâ± = tsimplify(eáµ¢ * transpose(gâ±Ê²))
                new{dim,T}(eáµ¢, eâ±, gáµ¢â±¼, gâ±Ê²)
            end
        end
    end
    function Basis(eâ±::AbstractMatrix{T}, ::Val{:cont}) where {T}
        dim = size(eâ±, 1)
        @assert dim == size(eâ±, 2) "v should be a square matrix"
        if isidentity(eâ±)
            return CanonicalBasis{dim,T}()
        else
            eâ± = Matrix(eâ±)
            gâ±Ê² = tsimplify(Symmetric(transpose(eâ±) * eâ±))
            if isidentity(gâ±Ê²)
                return RotatedBasis(eâ±)
            elseif isdiagonal(gâ±Ê²)
                uÏ‡ = inv.(sqrt.(diag(gâ±Ê²)))
                return OrthogonalBasis(RotatedBasis(eâ± .* transpose(uÏ‡)), uÏ‡)
            else
                if T == Sym
                    gáµ¢â±¼ = tsimplify(Symmetric(inv(Matrix(gâ±Ê²))))
                else
                    gáµ¢â±¼ = tsimplify(inv(gâ±Ê²))
                end
                eáµ¢ = tsimplify(eâ± * transpose(gáµ¢â±¼))
                new{dim,T}(eáµ¢, eâ±, gáµ¢â±¼, gâ±Ê²)
            end
        end
    end
    Basis(v::AbstractMatrix{T}, var::Symbol) where {T} = Basis(v, Val(var))
    Basis(v::AbstractMatrix{T}) where {T} = Basis(v, :cov)
    Basis(Î¸::T1, Ï•::T2, Ïˆ::T3 = 0) where {T1,T2,T3} = RotatedBasis(Î¸, Ï•, Ïˆ)
    Basis(Î¸::T) where {T} = RotatedBasis(Î¸)
    Basis{dim,T}() where {dim,T} = CanonicalBasis{dim,T}()
    Basis() = CanonicalBasis()
end


"""
    CanonicalBasis{dim, T}

Canonical basis of dimension `dim` (default: 3) and type `T` (default: Sym)

The attributes of this object can be obtained by
- `vecbasis(â„¬, :cov)`: square matrix defining the primal basis `eáµ¢=e[:,i]=Î´áµ¢â±¼`
- `vecbasis(â„¬, :cont)`: square matrix defining the dual basis `eâ±=E[:,i]=Î´áµ¢â±¼`
- `metric(â„¬, :cov)`: square matrix defining the covariant components of the metric tensor `gáµ¢â±¼=eáµ¢â‹…eâ±¼=g[i,j]=Î´áµ¢â±¼`
- `metric(â„¬, :cont)`: square matrix defining the contravariant components of the metric tensor `gâ±Ê²=eâ±â‹…eÊ²=gâ±Ê²[i,j]=Î´áµ¢â±¼`

# Examples
```julia
julia> â„¬ = CanonicalBasis()
CanonicalBasis{3, Sym}
# basis: 3Ã—3 TensND.LazyIdentity{3, Sym}:
 1  0  0
 0  1  0
 0  0  1
# dual basis: 3Ã—3 TensND.LazyIdentity{3, Sym}:
 1  0  0
 0  1  0
 0  0  1
# covariant metric tensor: 3Ã—3 TensND.LazyIdentity{3, Sym}:
 1  0  0
 0  1  0
 0  0  1
# contravariant metric tensor: 3Ã—3 TensND.LazyIdentity{3, Sym}:
 1  0  0
 0  1  0
 0  0  1

julia> â„¬â‚‚ = CanonicalBasis{2, Float64}()
CanonicalBasis{2, Float64}
# basis: 2Ã—2 TensND.LazyIdentity{2, Float64}:
 1.0  0.0
 0.0  1.0
# dual basis: 2Ã—2 TensND.LazyIdentity{2, Float64}:
 1.0  0.0
 0.0  1.0
# covariant metric tensor: 2Ã—2 TensND.LazyIdentity{2, Float64}:
 1.0  0.0
 0.0  1.0
# contravariant metric tensor: 2Ã—2 TensND.LazyIdentity{2, Float64}:
 1.0  0.0
 0.0  1.0
```
"""
struct CanonicalBasis{dim,T} <: AbstractBasis{dim,T} end
CanonicalBasis() = CanonicalBasis{3,Sym}()


"""
    RotatedBasis(Î¸::T<:Number, Ï•::T<:Number, Ïˆ::T<:Number)
    RotatedBasis(Î¸::T<:Number)

Orthonormal basis of dimension `dim` (default: 3) and type `T` (default: Sym) built from Euler angles `Î¸` in 2D and `(Î¸, Ï•, Ïˆ)` in 3D

# Examples
```julia
julia> Î¸, Ï•, Ïˆ = symbols("Î¸, Ï•, Ïˆ", real = true) ; â„¬Ê³ = RotatedBasis(Î¸, Ï•, Ïˆ) ; display(vecbasis(â„¬Ê³, :cov))
3Ã—3 Tensor{2, 3, Sym, 9}:
 -sin(Ïˆ)â‹…sin(Ï•) + cos(Î¸)â‹…cos(Ïˆ)â‹…cos(Ï•)  -sin(Ïˆ)â‹…cos(Î¸)â‹…cos(Ï•) - sin(Ï•)â‹…cos(Ïˆ)  sin(Î¸)â‹…cos(Ï•)
  sin(Ïˆ)â‹…cos(Ï•) + sin(Ï•)â‹…cos(Î¸)â‹…cos(Ïˆ)  -sin(Ïˆ)â‹…sin(Ï•)â‹…cos(Î¸) + cos(Ïˆ)â‹…cos(Ï•)  sin(Î¸)â‹…sin(Ï•)
                        -sin(Î¸)â‹…cos(Ïˆ)                          sin(Î¸)â‹…sin(Ïˆ)         cos(Î¸)
```
"""
struct RotatedBasis{dim,T} <: AbstractBasis{dim,T}
    eáµ¢::Matrix{T} # Primal basis `eáµ¢=e[:,i]`
    eâ±::Matrix{T} # Dual basis `eâ±=E[:,i]`
    angles::NamedTuple
    function RotatedBasis(R::AbstractMatrix{T}) where {T<:Number}
        dim = size(R, 1)
        if isidentity(R)
            return CanonicalBasis{dim,T}()
        else
            eáµ¢ = eâ± = Matrix(R)
            return new{dim,T}(eáµ¢, eâ±, angles(R))
        end
    end
    function RotatedBasis(Î¸::T1, Ï•::T2, Ïˆ::T3 = 0) where {T1<:Number,T2<:Number,T3<:Number}
        T = promote_type(T1, T2, T3)
        dim = 3
        R = RotZYZ(Ï•, Î¸, Ïˆ)
        if isidentity(R)
            return CanonicalBasis{dim,T}()
        else
            eáµ¢ = eâ± = Matrix(R)
            return new{dim,T}(eáµ¢, eâ±, angles(R))
        end
    end
    function RotatedBasis(Î¸::T) where {T<:Number}
        dim = 2
        cÎ¸ = cos(Î¸)
        sÎ¸ = sin(Î¸)
        eáµ¢ = eâ± = [cÎ¸ -sÎ¸; sÎ¸ cÎ¸]
        if isidentity(eáµ¢)
            return CanonicalBasis{dim,T}()
        else
            return new{dim,T}(eáµ¢, eâ±, angles(eáµ¢))
        end
    end
    function RotatedBasis(Î¸::Sym)
        dim = 2
        cÎ¸ = cos(Î¸)
        sÎ¸ = sin(Î¸)
        eáµ¢ = eâ± = [cÎ¸ -sÎ¸; sÎ¸ cÎ¸]
        if isidentity(eáµ¢)
            return CanonicalBasis{dim,Sym}()
        else
            return new{dim,Sym}(eáµ¢, eâ±, (Î¸ = Î¸,))
        end
    end
end


const OrthonormalBasis{dim,T} = Union{CanonicalBasis{dim,T},RotatedBasis{dim,T}}

struct OrthogonalBasis{dim,T} <: AbstractBasis{dim,T}
    parent::OrthonormalBasis{dim,T}
    Î»::Vector{T}
    eáµ¢::Matrix{T}
    eâ±::Matrix{T}
    gáµ¢â±¼::Diagonal{T,Vector{T}}
    gâ±Ê²::Diagonal{T,Vector{T}}
    function OrthogonalBasis(parent::OrthonormalBasis{dim,T}, Î»::Vector) where {dim,T}
        Î» = T.(Î»)
        if Î» == one.(Î»)
            return parent
        else
            eáµ¢ = [Î»[j] * parent[i, j] for i âˆˆ 1:dim, j âˆˆ 1:dim]
            eâ± = [parent[i, j] / Î»[j] for i âˆˆ 1:dim, j âˆˆ 1:dim]
            return new{dim,T}(parent, Î», eáµ¢, eâ±, Diagonal(Î» .^ 2), Diagonal(inv.(Î») .^ 2))
        end
    end
end

relevant_OrthonormalBasis(â„¬::OrthogonalBasis) = â„¬.parent
relevant_OrthonormalBasis(â„¬::OrthonormalBasis) = â„¬
relevant_OrthonormalBasis(::Basis{dim,T}) where {dim,T} = CanonicalBasis{dim,T}()


@inline CylindricalBasis(Î¸) = RotatedBasis(0, Î¸, 0)

@inline SphericalBasis(Î¸, Ï•) = RotatedBasis(Î¸, Ï•, 0)

const AllOrthogonalBasis{dim,T} = Union{OrthonormalBasis{dim,T},OrthogonalBasis{dim,T}}

angles(M::AbstractMatrix{T}, ::Val{2}) where {T} =
    (Î¸ = atan(M[2, 1] - M[1, 2], M[1, 1] + M[2, 2]),)
function angles(M::AbstractMatrix{T}, ::Val{3}) where {T}
    R = RotZYZ(M)
    return (Î¸ = R.theta2, Ï• = R.theta1, Ïˆ = R.theta3)
end

"""
    angles(M::AbstractMatrix{T})

Determine the Euler angles corresponding to the input matrix supposed to be a rotation matrix or at least a similarity

# Examples
```julia
julia> Î¸, Ï•, Ïˆ = symbols("Î¸, Ï•, Ïˆ", real = true) ; â„¬Ê³ = RotatedBasis(Î¸, Ï•, Ïˆ) ; display(vecbasis(â„¬Ê³, :cov))
3Ã—3 Tensor{2, 3, Sym, 9}:
 -sin(Ïˆ)â‹…sin(Ï•) + cos(Î¸)â‹…cos(Ïˆ)â‹…cos(Ï•)  -sin(Ïˆ)â‹…cos(Î¸)â‹…cos(Ï•) - sin(Ï•)â‹…cos(Ïˆ)  sin(Î¸)â‹…cos(Ï•)
  sin(Ïˆ)â‹…cos(Ï•) + sin(Ï•)â‹…cos(Î¸)â‹…cos(Ïˆ)  -sin(Ïˆ)â‹…sin(Ï•)â‹…cos(Î¸) + cos(Ïˆ)â‹…cos(Ï•)  sin(Î¸)â‹…sin(Ï•)
                        -sin(Î¸)â‹…cos(Ïˆ)                          sin(Î¸)â‹…sin(Ïˆ)         cos(Î¸)

julia> angles(â„¬Ê³)
(Î¸ = Î¸, Ï• = Ï•, Ïˆ = Ïˆ)
```
"""
angles(M::AbstractMatrix{T}) where {T} = angles(M, Val(size(M, 1)))
angles(â„¬::RotatedBasis) = â„¬.angles

angles(v::AbstractVector{T}, ::Val{2}) where {T} = (Î¸ = atan(v[2], v[1]),)
angles(v::AbstractVector{T}, ::Val{3}) where {T} =
    (Î¸ = atan(âˆš(v[1]^2 + v[2]^2), v[3]), Ï• = atan(v[2], v[1]))
angles(v::AbstractVector{T}) where {T} = angles(v, Val(size(v, 1)))


invvar(::Val{:cov}) = :cont
invvar(::Val{:cont}) = :cov
invvar(var) = invvar(Val(var))


"""
    vecbasis(â„¬::AbstractBasis, var = :cov)

Return the primal (if `var = :cov`) or dual (if `var = :cont`) basis
"""
vecbasis(â„¬::AbstractBasis, ::Val{:cov}) = â„¬.eáµ¢
vecbasis(â„¬::AbstractBasis, ::Val{:cont}) = â„¬.eâ±
vecbasis(::CanonicalBasis{dim,T}, ::Val{:cov}) where {dim,T} = Id2{dim,T}()
vecbasis(::CanonicalBasis{dim,T}, ::Val{:cont}) where {dim,T} = Id2{dim,T}()

vecbasis(â„¬::AbstractBasis, var) = vecbasis(â„¬, Val(var))
vecbasis(â„¬::AbstractBasis) = vecbasis(â„¬, :cov)
vecbasis(â„¬::AbstractBasis, i::Integer, j::Integer, var = :cov) = vecbasis(â„¬, Val(var))[i, j]
vecbasis(â„¬::AbstractBasis, i::Integer, var = :cov) = vecbasis(â„¬, Val(var))[:, i]

strvecbasis(::AbstractBasis, i::Integer, ::Val{:cov} ; vec = "ğ") = vec * subscriptnumber(i)
strvecbasis(::AbstractBasis, i::Integer, ::Val{:cont} ; vec = "ğ") = vec * superscriptnumber(i)
strvecbasis(â„¬::AbstractBasis, i::Integer, var = :cov ; vec = "ğ") = strvecbasis(â„¬, i, Val(var) ; vec = vec)

const dsubscriptchar = Dict(
    "a" => "â‚",
    "e" => "â‚‘",
    "h" => "â‚•",
    "i" => "áµ¢",
    "j" => "â±¼",
    "k" => "â‚–",
    "l" => "â‚—",
    "m" => "â‚˜",
    "n" => "â‚™",
    "o" => "â‚’",
    "p" => "â‚š",
    "r" => "áµ£",
    "s" => "â‚›",
    "t" => "â‚œ",
    "u" => "áµ¤",
    "v" => "áµ¥",
    "x" => "â‚“",
    "Î²" => "áµ¦",
    "Î³" => "áµ§",
    "Ï" => "áµ¨",
    "Ï•" => "áµ©",
    "Ï‡" => "áµª",
)

const dsuperscriptchar = Dict(
    "a" => "áµƒ",
    "b" => "áµ‡",
    "c" => "á¶œ",
    "d" => "áµˆ",
    "e" => "áµ‰",
    "f" => "á¶ ",
    "g" => "áµ",
    "h" => "Ê°",
    "i" => "â±",
    "j" => "Ê²",
    "k" => "áµ",
    "l" => "Ë¡",
    "m" => "áµ",
    "n" => "â¿",
    "o" => "á´¼",
    "p" => "áµ–",
    "r" => "Ê³",
    "s" => "Ë¢",
    "t" => "áµ—",
    "u" => "á¶¸",
    "v" => "áµ›",
    "w" => "Ê·",
    "x" => "Ë£",
    "y" => "Ê¸",
    "z" => "á¶»",
    "Î²" => "áµ",
    "Î³" => "áµ",
    "Îµ" => "áµ‹",
    "Î¸" => "á¶¿",
    "Î¹" => "á¶¥ ",
    "Ï•" => "áµ ",
    "Ï‡" => "áµ¡",
)

subscriptchar(s::String) = s âˆˆ keys(dsubscriptchar) ? dsubscriptchar[s] : s âˆˆ keys(dsuperscriptchar) ? dsuperscriptchar[s] : s
superscriptchar(s::String) = s âˆˆ keys(dsuperscriptchar) ? dsuperscriptchar[s] : s âˆˆ keys(dsubscriptchar) ? dsubscriptchar[s] : s


strvecbasis(::AbstractBasis, i::AbstractString, ::Val{:cov} ; vec = "ğ") = vec * subscriptchar(i)
strvecbasis(::AbstractBasis, i::AbstractString, ::Val{:cont} ; vec = "ğ") = vec * superscriptchar(i)
strvecbasis(â„¬::AbstractBasis, i::AbstractString, var = :cov ; vec = "ğ") = strvecbasis(â„¬, i, Val(var) ; vec = vec)


"""
    metric(â„¬::AbstractBasis, var = :cov)

Return the covariant (if `var = :cov`) or contravariant (if `var = :cont`) metric matrix
"""
metric(â„¬::AbstractBasis, ::Val{:cov}) = â„¬.gáµ¢â±¼
metric(â„¬::AbstractBasis, ::Val{:cont}) = â„¬.gâ±Ê²
metric(::OrthonormalBasis{dim,T}, ::Val{:cov}) where {dim,T} = Id2{dim,T}()
metric(::OrthonormalBasis{dim,T}, ::Val{:cont}) where {dim,T} = Id2{dim,T}()

metric(â„¬::AbstractBasis, var) = metric(â„¬, Val(var))
metric(â„¬::AbstractBasis) = metric(â„¬, :cov)
metric(â„¬::AbstractBasis, i::Integer, j::Integer, var = :cov) = metric(â„¬, Val(var))[i, j]

"""
    normalize(â„¬::AbstractBasis, var = cov)

Build a basis after normalization of column vectors of input matrix `v` where columns define either
- primal vectors ie `eáµ¢=v[:,i]/norm(v[:,i])` if `var = :cov` as by default
- dual vector ie `eâ±=v[:,i]/norm(v[:,i])` if `var = :cont`.
"""
function LinearAlgebra.normalize(â„¬::AbstractBasis, var = :cov)
    w = copy(vecbasis(â„¬, var))
    for i = 1:size(w, 2)
        w[:, i] /= norm(w[:, i])
    end
    return Basis(w, var)
end

"""
    isorthogonal(â„¬::AbstractBasis)

Check whether the basis `â„¬` is orthogonal
"""
isorthogonal(â„¬::AbstractBasis) = isdiagonal(metric(â„¬))

isorthogonal(::OrthonormalBasis) = true

isorthogonal(::OrthogonalBasis) = true

"""
    isorthonormal(â„¬::AbstractBasis)

Check whether the basis `â„¬` is orthonormal
"""
isorthonormal(â„¬::AbstractBasis) = isidentity(metric(â„¬))

isorthonormal(::OrthonormalBasis) = true

for OP in (:(tsimplify), :(tfactor), :(tsubs), :(tdiff), :(ttrigsimp), :(texpand_trig))
    @eval $OP(b::AbstractBasis{dim,Sym}, args...; kwargs...) where {dim} =
        Basis($OP(b.eáµ¢, args...; kwargs...))
    @eval $OP(b::CanonicalBasis{dim,Sym}, args...; kwargs...) where {dim} = b
end
for OP in (:(tsimplify), :(tsubs), :(tdiff))
    @eval $OP(b::AbstractBasis{dim,Num}, args...; kwargs...) where {dim} =
        Basis($OP(b.eáµ¢, args...; kwargs...))
    @eval $OP(b::CanonicalBasis{dim,Num}, args...; kwargs...) where {dim} = b
end


#####################
# Display Functions #
#####################
for OP in (:show, :print, :display)
    @eval begin
        function Base.$OP(â„¬::AbstractBasis)
            $OP(typeof(â„¬))
            print("â†’ basis: ")
            $OP(vecbasis(â„¬, :cov))
            print("â†’ dual basis: ")
            $OP(vecbasis(â„¬, :cont))
            print("â†’ covariant metric tensor: ")
            $OP(metric(â„¬, :cov))
            print("â†’ contravariant metric tensor: ")
            $OP(metric(â„¬, :cont))
        end
    end
end

export Basis, CanonicalBasis, RotatedBasis, CylindricalBasis, SphericalBasis, OrthogonalBasis, AllOrthogonalBasis
export getdim, vecbasis, metric, angles, isorthogonal, isorthonormal

