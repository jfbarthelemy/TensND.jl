@testsection "Coordinate systems" begin

    @testsection "Usual coordinate systems" begin
        (x, y, z), (­ЮљъРѓЂ, ­ЮљъРѓѓ, ­ЮљъРѓЃ), Рёг = init_canonical()
        (╬И, ¤Ћ, r), (­ЮљърХ┐, ­Юљърха, ­Юљъ╩│), Рёг╦б = init_spherical()
        @test components(­Юљъ╩│ РіЌ ­Юљърха, Рёг╦б) == components(­ЮљъРѓЃ РіЌ ­ЮљъРѓѓ, Рёг) == components_canon(­ЮљъРѓЃ РіЌ ­ЮљъРѓѓ)
    end

    @testsection "Partial derivatives" begin
        (x, y, z), (­ЮљъРѓЂ, ­ЮљъРѓѓ, ­ЮљъРѓЃ), Рёг = init_canonical()
        (╬И, ¤Ћ, r), (­ЮљърХ┐, ­Юљърха, ­Юљъ╩│), Рёг╦б = init_spherical()
        @test Рѕѓ(­Юљъ╩│, ╬И) == ­ЮљърХ┐
        @test Рѕѓ(­Юљъ╩│, ¤Ћ) == sin(╬И) * ­Юљърха
        @test Рѕѓ(­Юљърха РіЌ ­ЮљърХ┐, ¤Ћ) == Рѕѓ(­Юљърха, ¤Ћ) РіЌ ­ЮљърХ┐ + ­Юљърха РіЌ Рѕѓ(­ЮљърХ┐, ¤Ћ)
        @test Рѕѓ(­Юљъ╩│ РіЌ╦б ­Юљърха, ¤Ћ) == Рѕѓ(­Юљъ╩│, ¤Ћ) РіЌ╦б ­Юљърха + ­Юљъ╩│ РіЌ╦б Рѕѓ(­Юљърха, ¤Ћ)
    end

    @testsection "Coordinate systems" begin
        coords, vectors, Рёг╦б = init_spherical()
        ╬И, ¤Ћ, r = coords
        ­ЮљърХ┐, ­Юљърха, ­Юљъ╩│ = vectors
        OM = r * ­Юљъ╩│
        CS = CoorSystemSym(OM, coords; simp = Dict(abs(sin(╬И)) => sin(╬И)))
        ¤Ѓ╩│╩│ = SymFunction("¤Ѓ╩│╩│", real = true)(r)
        ¤ЃрХ┐рХ┐ = SymFunction("¤ЃрХ┐рХ┐", real = true)(r)
        ¤Ѓрхарха = SymFunction("¤Ѓрхарха", real = true)(r)
        ­ЮЏћ = ¤Ѓ╩│╩│ * ­Юљъ╩│ РіЌ ­Юљъ╩│ + ¤ЃрХ┐рХ┐ * ­ЮљърХ┐ РіЌ ­ЮљърХ┐ + ¤Ѓрхарха * ­Юљърха РіЌ ­Юљърха
        div­ЮЏћ = DIV(­ЮЏћ, CS)
        @test simplify(div­ЮЏћ РІЁ ­Юљъ╩│) == simplify(diff(¤Ѓ╩│╩│, r) + (2 * ¤Ѓ╩│╩│ - ¤ЃрХ┐рХ┐ - ¤Ѓрхарха) / r) 

    end


end
